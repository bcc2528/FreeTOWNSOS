; INT 93H Disk BIOS

						CPU		386
						BITS	16

						%INCLUDE "UTIL.NSM"
						%INCLUDE "DEF.NSM"
						%INCLUDE "TOWNSDEF.NSM"
						%INCLUDE "IODEF.NSM"


CODE					SEGMENT USE16

INT93H:
						SAVE_WORLD
						MOV		BP,SP

						MOV		BYTE CS:[ERROR_CODE],0

						CMP		AH,BIOSCMD_COMMON_INIT
						JE		INT93H_INIT

						AND		AL,0F0H
						CMP		AL,DISKBIOS_FDD_0
						JE		INT93H_FD
						CMP		AL,DISKBIOS_SCSI_0
						JE		INT93H_HD
						CMP		AL,DISKBIOS_CD
						JE		INT93H_CD

						MOV		BYTE CS:[ERROR_CODE],DISKBIOS_ERROR_WRONG_DEVICE_OR_MODE

INT93H_EXIT:
						RESTORE_WORLD

						MOV		AH,CS:[ERROR_CODE]
						CMP		AH,80H
						JNE		INT93H_NOT_HARD_ERROR
						MOV		CX,CS:[ERROR_DETAIL]
INT93H_NOT_HARD_ERROR:
						AND		AH,AH
						CLC
						JE		INT93H_IRET
						STC
INT93H_IRET:
						IRET



INT93H_EXIT_NO_ERROR:
						MOV		BYTE CS:[ERROR_CODE],0
						JMP		INT93H_EXIT



ERROR_CODE				DB		0
ERROR_DETAIL			DW		0
INT46H_DID_COME_IN		DB		0


INT93H_INIT:
						XOR		AX,AX
						MOV		DS,AX

						MOV		AX,CS
						SHL		EAX,16

						MOV		AX,INT46H_FD_INT_HANDLER
						MOV		[046H*4],EAX

						MOV		AX,INT48H_SCSI_INT_HANDLER
						MOV		[048H*4],EAX

						MOV		AX,INT49H_CD_INT_HANDLER
						MOV		[049H*4],EAX

						; Hope PIC is in general set up by the IPL.

						IN		AL,IO_PIC_PRIMARY_MASK
						AND		AL,10111111b
						OUT		IO_PIC_PRIMARY_MASK,AL

						IN		AL,IO_PIC_SECONDARY_MASK
						AND		AL,11111100b
						OUT		IO_PIC_SECONDARY_MASK,AL

						JMP		INT93H_EXIT_NO_ERROR


INT46H_FD_INT_HANDLER:
						MOV		BYTE CS:[INT46H_DID_COME_IN],0FFh

						; Clear IRR
						PUSH	DX
						PUSH	AX
						MOV		DX,IO_FDC_STATUS
						IN		AL,DX
						MOV		DX,IO_DMA_STATUS ; Do I have to do it?
						IN		AL,DX

						MOV		AL,066H	; Specific EOI(060H)|INT 6(006H)
						OUT		IO_PIC0_OCW2,AL

						POP		AX
						POP		DX

						IRET

INT48H_SCSI_INT_HANDLER:
						IRET

INT49H_CD_INT_HANDLER:
						IRET

						%INCLUDE "INT93H_FD.NSM"
						%INCLUDE "INT93H_HD.NSM"
						%INCLUDE "INT93H_CD.NSM"
						%INCLUDE "INT93H_M.NSM"
						%INCLUDE "INT93H_DMA.NSM"
