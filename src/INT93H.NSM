; INT 93H Disk BIOS

						CPU		386
						BITS	16

						%INCLUDE "UTIL.NSM"
						%INCLUDE "DEF.NSM"
						%INCLUDE "TOWNSDEF.NSM"
						%INCLUDE "IODEF.NSM"


CODE					SEGMENT USE16

INT93H:
						SAVE_WORLD
						MOV		BP,SP

						MOV		BYTE CS:[ERROR_CODE],0

						CMP		AH,BIOSCMD_COMMON_INIT
						JE		INT93H_INIT

						AND		AL,0F0H
						CMP		AL,DISKBIOS_FDD_0
						JE		INT93H_FD
						CMP		AL,DISKBIOS_SCSI_0
						JE		INT93H_HD
						CMP		AL,DISKBIOS_CD
						JE		INT93H_CD
						CMP		AL,DISKBIOS_ROMDISK
						JE		INT93H_ROMDISK


						MOV		BYTE CS:[ERROR_CODE],DISKBIOS_ERROR_WRONG_DEVICE_OR_MODE

INT93H_EXIT:
						RESTORE_WORLD

						MOV		AH,CS:[ERROR_CODE]
						CMP		AH,80H
						JNE		INT93H_NOT_HARD_ERROR
						MOV		CX,CS:[ERROR_DETAIL]
INT93H_NOT_HARD_ERROR:
						AND		AH,AH
						CLC
						JE		INT93H_IRET
						STC
INT93H_IRET:
						IRET



INT93H_EXIT_NO_ERROR:
						MOV		BYTE CS:[ERROR_CODE],0
						JMP		INT93H_EXIT



ERROR_CODE				DB		0
ERROR_DETAIL			DW		0
INT46H_DID_COME_IN		DB		0


INT93H_INIT:
						XOR		AX,AX
						MOV		DS,AX

						MOV		CS:[INT46H_FD_INT_DATA_BLOCK+4],CS
						MOV		CS:[INT48H_SCSI_INT_DATA_BLOCK+4],CS
						MOV		CS:[INT49H_CD_INT_DATA_BLOCK+4],CS

						PUSH	CS
						POP		DS
						MOV		DI,INT46H_FD_INT_DATA_BLOCK
						MOV		DL,6
						XOR		AH,AH
						INT		0AEH

						MOV		DI,INT48H_SCSI_INT_DATA_BLOCK
						MOV		DL,8
						XOR		AH,AH
						INT		0AEH

						MOV		DI,INT49H_CD_INT_DATA_BLOCK
						MOV		DL,9
						XOR		AH,AH
						INT		0AEH

						PUSH	EAX	; Make buffer in STACK
						MOV		DI,SS
						MOV		DS,DI
						MOV		DI,SP
						MOV		AH,03H	; Get INT-Enabled Flags
						INT		0AEH

						MOV		EAX,DS:[DI]
						OR		EAX,INTMANBIOS_INT06_FLAG+INTMANBIOS_INT08_FLAG+INTMANBIOS_INT09_FLAG
						MOV		DS:[DI],EAX

						MOV		AH,02H	; Set INT-Enabled Flags
						INT		0AEH

						POP		EAX

						JMP		INT93H_EXIT_NO_ERROR

INT46H_FD_INT_DATA_BLOCK:
						DB		0,0
						DW		INT46H_FD_INT_HANDLER
						DW		0

INT46H_FD_INT_HANDLER:
						MOV		BYTE CS:[INT46H_DID_COME_IN],0FFh

						; Clear IRR
						MOV		DX,IO_FDC_STATUS
						IN		AL,DX
						MOV		DX,IO_DMA_STATUS ; Do I have to do it?
						IN		AL,DX

						RETF

INT48H_SCSI_INT_DATA_BLOCK:
						DB		0,0
						DW		INT48H_SCSI_INT_HANDLER
						DW		0

INT48H_SCSI_INT_HANDLER:
						RETF

INT49H_CD_INT_DATA_BLOCK:
						DB		0,0
						DW		INT49H_CD_INT_HANDLER
						DW		0

INT49H_CD_INT_HANDLER:
						RETF

						%INCLUDE "INT93H_FD.NSM"
						%INCLUDE "INT93H_HD.NSM"
						%INCLUDE "INT93H_CD.NSM"
						%INCLUDE "INT93H_M.NSM"
						%INCLUDE "INT93H_RM.NSM"
						%INCLUDE "INT93H_DMA.NSM"
						%INCLUDE "RDHIGH.NSM"
