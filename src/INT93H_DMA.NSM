
; Input
;   AL  DMA Channel
;   EDX Physical Address
;   CX  Count
INT93H_DMA_SETUP_READ:
						PUSH	BX
						MOV		BL,44h					; Single Mode, I/O->MEM
						CALL	INT93H_DMA_SETUP_COMMON
						POP		BX
						RET


INT93H_DMA_SETUP_WRITE:
						PUSH	BX
						MOV		BL,48h					; Single Mode, MEM->I/O
						CALL	INT93H_DMA_SETUP_COMMON
						POP		BX
						RET


INT93H_DMA_SETUP_COMMON:
						PUSH	AX
						PUSH	CX

						OUT		IO_DMA_CHANNEL,AL		; 00A1h

						XCHG	AX,CX					; Now CX is channel
						OUT		IO_DMA_COUNT_LOW,AX		; 00A2h

						MOV		AX,DX
						OUT		IO_DMA_ADDR_LOW,AX		; 00A4h

						ROL		EDX,16
						MOV		AX,DX
						OUT		IO_DMA_ADDR_MID_HIGH,AL ; 00A6h
						MOV		AL,AH
						OUT		IO_DMA_ADDR_HIGH,AL     ; 00A7h
						ROL		EDX,16

						MOV		AL,BL
						OUT		IO_DMA_MODE_CONTROL,AL	; 00AAh

						MOV		AH,1
						AND		CL,3
						SHL		AH,CL
						NOT		AH
						IN		AL,IO_DMA_MASK
						AND		AL,AH
						AND		AL,0Fh
						OUT		IO_DMA_MASK,AL		; 00AFh

						POP		CX
						POP		AX
						RET


; Input
;   AL DMA Channel
INT93H_DMA_MASK:
						PUSH	AX
						PUSH	CX

						MOV		CL,AL
						AND		CL,3
						MOV		AH,1
						SHL		AH,CL

						IN		AL,IO_DMA_MASK
						OR		AL,AH
						AND		AL,0Fh
						OUT		IO_DMA_MASK,AL		; 00AFh

						POP		CX
						POP		AX
						RET
