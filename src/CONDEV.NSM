CONDEV_HEADER_NEXT			DW		CLKDEV_HEADER_NEXT
							DW		IOSYS_CS
CONDEV_HEADER_ATTRIBWORD	DW		8003h
CONDEV_HEADER_STRATEGY_PTR	DW		CONDEV_STRATEGY
CONDEV_HEADER_INTERRUPT_PTR	DW		CONDEV_INTERRUPT
CONDEV_HEADER_NAME			DB		"CON     "

CONDEV_REQHDR_PTR		DD	0

CONDEV_STRATEGY:
						MOV		CS:[CONDEV_REQHDR_PTR],BX
						MOV		CS:[CONDEV_REQHDR_PTR+2],ES
						RETF

CONDEV_INTERRUPT:
						SAVE_WORLD

						LES		BX,CS:[CONDEV_REQHDR_PTR]

						MOV		WORD ES:[BX+REQ_STATUS],DEVREQ_STATUS_NOERROR ; Tentatively no error
						MOV		AL,ES:[BX+REQ_COMMAND]
						CMP		AL,DEVREQ_CMD_INIT
						JE		CONDEV_INIT
						CMP		AL,DEVREQ_CMD_READ
						JE		CONDEV_READ
						CMP		AL,DEVREQ_CMD_WRITE
						JE		CONDEV_WRITE
						CMP		AL,DEVREQ_CMD_WRITE_VERIFY
						JE		CONDEV_WRITE
						CMP		AL,DEVREQ_CMD_NON_DEST_READ
						JE		CONDEV_NON_DESTRUCTIVE_READ

						CMP		AL,DEVREQ_CMD_FLUSH_INPUT
						JE		CONDEV_FLUSH_INPUT

						CMP		AL,DEVREQ_CMD_OUTPUT_STATUS
						JE		CONDEV_INTERRUPT_RETURN
						CMP		AL,DEVREQ_CMD_INPUT_STATUS
						JE		CONDEV_INTERRUPT_RETURN


						TSUGARU_DEBUG

CONDEV_INTERRUPT_RETURN:
						RESTORE_WORLD
						RETF

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CONDEV_INIT:

						JMP		CONDEV_INTERRUPT_RETURN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CONDEV_READ:
						MOV		WORD ES:[BX+REQ_READ_WRITE_SECTOR_COUNT],1 ; Is it always one byte at a time?

						MOV		CX,ES:[BX+REQ_READ_WRITE_SECTOR_COUNT]
						LDS		DI,ES:[BX+REQ_READ_WRITE_BUFFER_PTR]
						STI
CONDEV_READ_LOOP:
						MOV		AX,0900H	; READ (AH=09H) with Blocking (AL=00H)
						INT		90H

						; ES:BX no longer points to the request header.

						CMP		DH,0FFH
						JE		CONDEV_READ_LOOP

						MOV		[DI],DL
						INC		DI
						LOOP	CONDEV_READ_LOOP

						JMP		CONDEV_INTERRUPT_RETURN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CONDEV_NON_DESTRUCTIVE_READ:
						LDS		DI,ES:[BX+REQ_READ_WRITE_BUFFER_PTR]
						STI

						MOV		AH,07H
						INT		90H

						; ES:BX no longer points to the request header.

						CMP		DH,0FFH
						JE		CONDEV_NON_DESTRUCTIVE_READ_NO_INPUT

						OR		DL,DL
						JE		CONDEV_NON_DESTRUCTIVE_READ_NO_INPUT

						LES		BX,CS:[CONDEV_REQHDR_PTR]	; BX may be destroyed by now.
						MOV		ES:[BX+0DH],DL
						JMP		CONDEV_INTERRUPT_RETURN


CONDEV_NON_DESTRUCTIVE_READ_NO_INPUT:
						LES		BX,CS:[CONDEV_REQHDR_PTR]	; BX may be destroyed.
						MOV		WORD ES:[BX+REQ_STATUS],CONSDEV_FLAG_BUSY
						JMP		CONDEV_INTERRUPT_RETURN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CONDEV_FLUSH_INPUT:
						MOV		AH,06H ; Clear buffer
						INT		90H
						JMP		CONDEV_INTERRUPT_RETURN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CONDEV_WRITE:
						; Just for test. Always write "A\n"
						LDS		DI,ES:[BX+REQ_READ_WRITE_BUFFER_PTR]
						MOV		AL,DS:[DI]
						MOV		AH,1DH	; Console BIOS 1-char Output
						INT		91H
						JMP		CONDEV_INTERRUPT_RETURN

