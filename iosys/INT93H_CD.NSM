%macro					CDC_ZERO_PARAMETER	0
						XOR		EAX,EAX
						MOV		CS:[CDC_PARAM],EAX
						MOV		CS:[CDC_PARAM+4],EAX
%endmacro


CD_EMULATION_SCSI_EN	DB		0	; SCSI-CD Emulation enabled.
CD_EMULATION_SCSI_DEV	DB		0	; BIOS Device CODE.  0B?H for SCSI ID ?.

; Disc Change should be cached because the disc-change status code may be consumed by any of the BIOS function,
; such as CDDA functions.  If the disc-change status code is consumed before MSCDEX checks the disc change,
; it will not have a way to know the disc has changed unless the flag is cached.
DISC_CHANGE_FLAG_CACHE	DB		0


CDC_PARAM				DB		0,0,0,0,0,0,0,0
CDC_STATUS				DB		0,0,0,0
CDC_READ_POINTER_PHYS	DD		0

INT49H_CD_INT_HANDLER:
						RETF


; Extension to INT 93H AL=C?H
; YSSCSI.SYS Extension
; AX=73C0H  BX=0  CX=0  YSSCSICD installation check.
;   Returns BX=7711H CX=6809H if YSSCSICD is installed already.
;   TGBIOS should return BX=7710H CX=6809H if CD to SCSI redirection is enabled.
;
; AX=71C0H Play CDDA
; AX=72C0H Play CDDA BCD
;     BL Starting time M (In BCD if AH=72H)
;     BH Starting time S (In BCD if AH=72H)
;     CL Starting time F (In BCD if AH=72H)
;     CH Ending time M (In BCD if AH=72H)
;     DL Ending time S (In BCD if AH=72H)
;     DH Ending time F (In BCD if AH=72H)
; 
; TGBIOS Extension
; AX=74C0H Enable SCSI redirection
;     DL SCSI ID or FFH for auto-scan descending order or FEH for auto-scan ascending order.




INT93H_CD:
						MOV		BYTE CS:[ERROR_CODE],02H	; Tentative Wrong Device ID
						MOV		WORD CS:[ERROR_DETAIL],0FFFFh
						MOV		AX,[BP+SAVED_EAX]

						CMP		AL,0C0H
						JNE		INT93H_EXIT	; Device ID error

						MOV		BYTE CS:[ERROR_CODE],80H	; Tentative Hard Error

						CMP		BYTE CS:[CD_EMULATION_SCSI_EN],0
						JNE		INT93H_CD_EMU_BY_SCSI

						CMP		AH,DISKBIOS_CMD_EXT_ENABLE_SCSI_REDIR
						JE		INT93H_CD_ENABLE_EMULATION
						CMP		AH,DISKBIOS_CMD_EXT_DISABLE_SCSI_REDIR
						JE		INT93H_CD_DISABLE_EMULATION

						CMP		AH,DISKBIOS_CMD_EXT_GET_CDDA_STATE
						JE		INT93H_DISKBIOS_CD_EXT_8xH
						CMP		AH,DISKBIOS_CMD_EXT_GET_TOC
						JNE		INT93H_CD_CONTINUE

; Function 8xH will be converted to 5xH, but EDI is taken as DS|DI.
INT93H_DISKBIOS_CD_EXT_8xH:
						ROL		EDI,16
						MOV		DS,DI
						ROL		EDI,16
						SUB		AH,30H	; Make it 53H or 54H

INT93H_CD_CONTINUE:
						MOVZX	BX,AH
						CMP		BX,0FH
						JE		INT93H_CD_0FH_CHECK_DISC_CHANGE
						CMP		BX,14H
						JE		INT93H_CD_14H_SEEK_MSF
						CMP		BX,15H
						JE		INT93H_CD_15H_READ_MSF

						MOV		SI,INT93H_CD_JUMPTABLE
						CMP		BX,05H
						JBE		INT93H_CD_JUMP_TO_FUNCTION

						MOV		SI,INT93H_CD_JUMPTABLE_CDDA
						SUB		BX,50H
						CMP		BX,06H
						JBE		INT93H_CD_JUMP_TO_FUNCTION

						JMP		INT93H_EXIT


INT93H_CD_EMU_BY_SCSI:
						MOVZX	BX,BYTE [BP+SAVED_EAX+1]
						CMP		BX,0FH
						JE		INT93H_CD_EMU_0FH_CHECK_DISC_CHANGE
						CMP		BX,14H
						JE		INT93H_CD_EMU_14H_SEEK_MSF
						CMP		BX,15H
						JE		INT93H_CD_EMU_15H_READ_MSF

						MOV		SI,INT93H_CD_EMU_JUMPTABLE
						CMP		BX,05H
						JBE		INT93H_CD_JUMP_TO_FUNCTION

						MOV		SI,INT93H_CD_EMU_JUMPTABLE_CDDA
						SUB		BX,50H
						CMP		BX,06H
						JBE		INT93H_CD_JUMP_TO_FUNCTION

						TSUGARU_DEBUG

						JMP		INT93H_EXIT


INT93H_CD_JUMP_TO_FUNCTION:
						SHL		BX,1
						JMP		[CS:SI+BX]


INT93H_CD_JUMPTABLE:
						DW		INT93H_CD_00H_SET_DRVMODE	; Do nothing
						DW		INT93H_CD_01H_GET_DRVMODE	; Do nothing
						DW		INT93H_CD_02H_GET_DRVSTATUS
						DW		INT93H_CD_03H_RESTORE
						DW		INT93H_CD_04H_SEEK_LBA
						DW		INT93H_CD_05H_READ_LBA

						DW		INT93H_CD_14H_SEEK_MSF
						DW		INT93H_CD_15H_READ_MSF

INT93H_CD_JUMPTABLE_CDDA:
						DW		INT93H_CD_50H_CDDA_PLAY
						DW		INT93H_CD_51H_CDDA_GET_INFO
						DW		INT93H_CD_52H_CDDA_STOP
						DW		INT93H_CD_53H_CDDA_GET_STATUS
						DW		INT93H_CD_54H_CDDA_GET_CDINFO
						DW		INT93H_CD_55H_CDDA_PAUSE
						DW		INT93H_CD_56H_CDDA_RESUME



INT93H_CD_EMU_JUMPTABLE:
						DW		INT93H_CD_00H_SET_DRVMODE	; Do nothing
						DW		INT93H_CD_01H_GET_DRVMODE	; Do nothing
						DW		INT93H_CD_EMU_02H_GET_DRVSTATUS
						DW		INT93H_CD_EMU_03H_RESTORE
						DW		INT93H_CD_EMU_04H_SEEK_LBA
						DW		INT93H_CD_EMU_05H_READ_LBA

						DW		INT93H_CD_EMU_14H_SEEK_MSF
						DW		INT93H_CD_EMU_15H_READ_MSF

INT93H_CD_EMU_JUMPTABLE_CDDA:
						DW		INT93H_CD_EMU_50H_CDDA_PLAY
						DW		INT93H_CD_EMU_51H_CDDA_GET_INFO
						DW		INT93H_CD_EMU_52H_CDDA_STOP
						DW		INT93H_CD_EMU_53H_CDDA_GET_STATUS
						DW		INT93H_CD_EMU_54H_CDDA_GET_CDINFO
						DW		INT93H_CD_EMU_55H_CDDA_PAUSE
						DW		INT93H_CD_EMU_56H_CDDA_RESUME


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Input EAX Error code.
INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT:
						CMP		AH,09H
						JE		INT93H_CD_CDC_DRIVE_NOT_READY
						CMP		AH,08H
						JE		INT93H_CD_CDC_MEDIA_CHANGED

						; Unknown error
						MOV		BYTE CS:[ERROR_CODE],80H
						MOV		WORD CS:[ERROR_DETAIL],0
						JMP		INT93H_EXIT

INT93H_CD_CDC_DRIVE_NOT_READY:
						MOV		BYTE CS:[ERROR_CODE],80H
						MOV		WORD CS:[ERROR_DETAIL],1
						MOV		BYTE CS:[DISC_CHANGE_FLAG_CACHE],1
						JMP		INT93H_EXIT

INT93H_CD_CDC_MEDIA_CHANGED:
						MOV		BYTE CS:[ERROR_CODE],80H
						MOV		WORD CS:[ERROR_DETAIL],1
						MOV		BYTE CS:[DISC_CHANGE_FLAG_CACHE],1
						JMP		INT93H_EXIT


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_00H_SET_DRVMODE:
						MOV		BYTE CS:[ERROR_CODE],0
						; I tried different drive mode on real MX, but it did nothing.
						; I hoped to be able to read CDDA sectors, but I was too optimistic.
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_01H_GET_DRVMODE:
						MOV		BYTE CS:[ERROR_CODE],0
						MOV		BYTE [BP+SAVED_EDX],04H	; 2048-bytes per sector.
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_02H_GET_DRVSTATUS: ; It says Drive Status, but is rather Disc status.
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_03H_RESTORE:
						MOV		BYTE CS:[ERROR_CODE],0	; In fact, it does not have to do anything.
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_04H_SEEK_LBA:
						MOV		BYTE CS:[ERROR_CODE],0	; In fact, it does not have to do anything.
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_05H_READ_LBA:
						MOV		BX,[BP+SAVED_EBX]	; BX is destroyed.  Recover it.

						MOVZX	CX,CL
						SHL		ECX,16
						MOV		CX,DX	; ECX is LBA

						; But, in TOWNS, the first sector needs to be 00:02:00, add 150 (75 frames times 2) for 2 seconds.

						ADD		ECX,150	; ECX is the first sector.
						MOVZX	EBX,BX
						ADD		EBX,ECX
						DEC		EBX		; EBX is the last sector.

						MOV		EDX,EBX
						SHR		EDX,16
						MOV		AX,BX	; DX:AX is the last sector
						CALL	HSG2MSF
						MOV		CS:[CDC_PARAM+3],DL
						MOV		CS:[CDC_PARAM+4],AH
						MOV		CS:[CDC_PARAM+5],AL

						MOV		EDX,ECX
						SHR		EDX,16
						MOV		AX,CX	; DX:AX is the first sector
						CALL	HSG2MSF
						MOV		CS:[CDC_PARAM  ],DL
						MOV		CS:[CDC_PARAM+1],AH
						MOV		CS:[CDC_PARAM+2],AL

						MOV		WORD CS:[CDC_PARAM+6],0

						; Tentative
						MOV		CS:[CDC_READ_POINTER_PHYS],DI
						MOV		CS:[CDC_READ_POINTER_PHYS+2],SI

						MOV		CH,[BP+SAVED_ECX+1]
						CMP		CH,0FFH
						JE		INT93H_CD_REALLY_READ

						MOVZX	ESI,WORD [BP+SAVED_DS]
						SHL		ESI,4
						MOVZX	EDI,WORD [BP+SAVED_EDI]
						ADD		ESI,EDI

						MOV		CS:[CDC_READ_POINTER_PHYS],ESI



; 14H will also jump here.
INT93H_CD_REALLY_READ:
						CLI

						CALL	CDC_WAIT_READY	; It destroys AX and DX
						CALL	CDC_PUSH_PARAMETERS_BCD

						MOV		DX,IO_CDC_COMMAND
						MOV		AL,CDCCMD_MODE1_READ+CDCCMD_IRQ+CDCCMD_STATUSREQ
						OUT		DX,AL



INT93H_CD_READ_LOOP:
						CALL	CDC_GET_STATUS
						CMP		AL,06H	; Read Done
						JE		INT93H_CD_READ_READ_DONE
						CMP		AL,21H ; Error
						JE		INT93H_CD_READ_ERROR
						CMP		AL,22H ; Data Ready
						JNE		INT93H_CD_READ_LOOP

						; Data Ready
						CALL	CDC_SETUP_DMA_FOR_ONE_SECTOR

						MOV		DX,IO_CDC_TFR_CONTROL
						MOV		AL,010H	; DMA Transfer
						OUT		DX,AL

						CALL	CDC_WAIT_DMAE

						ADD		DWORD CS:[CDC_READ_POINTER_PHYS],0800H

						IN		AL,IO_DMA_MASK	; Mask DMA
						AND		AL,0FH
						OR		AL,08H
						OUT		IO_DMA_MASK,AL

						JMP		INT93H_CD_READ_LOOP

INT93H_CD_READ_READ_DONE:

						MOV		DX,IO_CDC_TFR_CONTROL	; Just in case.  Apparently DTS flag is cleared from the CDC.
						XOR		AL,AL
						OUT		DX,AL

						; Mask DMA
						IN		AL,IO_DMA_MASK
						AND		AL,0FH
						OR		AL,08H
						OUT		IO_DMA_MASK,AL

						; Clear DMA_ADDR_HIGH Just in case
						XOR		AL,AL
						OUT		IO_DMA_ADDR_HIGH,AL

						XOR		EAX,EAX

						MOV		BYTE CS:[ERROR_CODE],0

						JMP		INT93H_EXIT

INT93H_CD_READ_ERROR:
						JMP		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_0FH_CHECK_DISC_CHANGE:
						CLI


INT93H_CD_0FH_CHECK_DISC_CHANGE_CLEAR_STATUS:
						CALL	CDC_GET_STATUS
						JNC		INT93H_CD_0FH_CHECK_DISC_CHANGE_CLEAR_STATUS


						CALL	CDC_WAIT_READY	; It destroys AX and DX
						CDC_ZERO_PARAMETER		; It destroys EAX
						CALL	CDC_PUSH_PARAMETERS

						MOV		DX,IO_CDC_COMMAND
						MOV		AL,CDCCMD_GET_STATE+CDCCMD_STATUSREQ
						OUT		DX,AL

INT93H_CD_0FH_CHECK_DISC_CHANGE_WAIT_STATUS:
						CALL	CDC_GET_STATUS
						JC		INT93H_CD_0FH_CHECK_DISC_CHANGE_WAIT_STATUS

						CMP		AX,0800H	; Media Changed
						JE		INT93H_CD_0FH_DISC_DID_CHANGE

						CMP		AX,0900H	; 00 09 00 00 Drive Not Ready
						JE		INT93H_CD_0FH_DISC_DID_CHANGE	; Take it as a media change.

						CMP		AL,21H
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT

						XOR		DL,DL
						XCHG	DL,CS:[DISC_CHANGE_FLAG_CACHE]

						MOV		BYTE CS:[ERROR_CODE],0
						MOV		BYTE [BP+SAVED_EDX],DL
						JMP		INT93H_EXIT

INT93H_CD_0FH_DISC_DID_CHANGE:
						MOV		BYTE CS:[DISC_CHANGE_FLAG_CACHE],0
						MOV		BYTE CS:[ERROR_CODE],0
						MOV		BYTE [BP+SAVED_EDX],1
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_14H_SEEK_MSF:
						MOV		BYTE CS:[ERROR_CODE],0	; In fact, it does not have to do anything.
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_15H_READ_MSF:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_50H_CDDA_PLAY:
						MOV		CX,[BP+SAVED_ECX]
						AND		CH,CH
						JE		CDDA_PLAY_CH_CHECKED
						CMP		CH,0FEH
						JB		INT93H_EXIT_PAREMETER_ERROR

CDDA_PLAY_CH_CHECKED:
						MOV		DS,[BP+SAVED_DS]
						MOV		DI,[BP+SAVED_EDI]

						XOR		EAX,EAX
						MOV		AH,DS:[DI]
						SHL		EAX,8
						MOV		AX,DS:[DI+1]
						XCHG	AL,AH

						XOR		EDX,EDX
						MOV		DH,DS:[DI+3]
						SHL		EDX,8
						MOV		DX,DS:[DI+4]
						XCHG	DL,DH

						CMP		EAX,EDX
						JAE		INT93H_EXIT_PAREMETER_ERROR
						CMP		EAX,000200H
						JB		INT93H_EXIT_PAREMETER_ERROR
						CMP		EDX,000200H
						JB		INT93H_EXIT_PAREMETER_ERROR

						MOV		EAX,DS:[DI]
						MOV		CS:[CDC_PARAM],EAX
						MOV		AX,DS:[DI+4]
						MOV		CS:[CDC_PARAM+4],AX

						XOR		AX,AX
						AND		CH,CH
						JE		CDDA_PLAY_NO_REPEAT		; I don't know how to do repeat N times.
						MOV		AL,1					; Set repeat flag
CDDA_PLAY_NO_REPEAT:
						MOV		CS:[CDC_PARAM+6],AX

						CALL	CDC_WAIT_READY	; It destroys AX and DX
						CALL	CDC_PUSH_PARAMETERS_BCD

						MOV		DX,IO_CDC_COMMAND
						MOV		AL,CDCCMD_CDDAPLAY+CDCCMD_IRQ+CDCCMD_STATUSREQ
						OUT		DX,AL

CDDA_PLAY_STATUS_LOOP:
						CALL	CDC_GET_STATUS

						CMP		AL,21H		; Error
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT

						CMP		AL,00H		; Command done
						JNE		CDDA_PLAY_STATUS_LOOP

						JMP		INT93H_EXIT_NO_ERROR

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_51H_CDDA_GET_INFO:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_52H_CDDA_STOP:
						CLI

						CALL	CDC_WAIT_READY	; It destroys AX and DX

						CDC_ZERO_PARAMETER		; It destroys EAX
						CALL	CDC_PUSH_PARAMETERS_BCD

						MOV		DX,IO_CDC_COMMAND
						MOV		AL,CDCCMD_CDDA_STOP+CDCCMD_IRQ+CDCCMD_STATUSREQ
						OUT		DX,AL

INT93H_CD_52H_CDDA_STOP_LOOP:
						CALL	CDC_GET_STATUS

						CMP		AX,0900H	; Drive Not Ready
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT
						CMP		AX,0800H
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT

						TEST	AL,AL
						JE		INT93H_CD_52H_CDDA_STOP_LOOP

						CMP		AL,21H		; Error
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT

						CMP		AL,011H		; Stop done
						JNE		INT93H_CD_52H_CDDA_STOP_LOOP

						OUT		IO_1US_WAIT,AL
						OUT		IO_1US_WAIT,AL
						CALL	CDC_GET_STATUS	; This command may return 00 0D 00 00.  The meaning unknown.  I just want to clear status queue.

						OUT		IO_1US_WAIT,AL
						OUT		IO_1US_WAIT,AL
						CALL	CDC_GET_STATUS	; This command may return 00 0D 00 00.  The meaning unknown.  I just want to clear status queue.

						MOV		BYTE CS:[ERROR_CODE],00H

						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_53H_CDDA_GET_STATUS:
						MOV		CX,[BP+SAVED_ECX]
						OR		CH,CH
						JNE		INT93H_EXIT_PAREMETER_ERROR

						CLI

						CALL	CDC_WAIT_READY	; It destroys AX and DX
						CDC_ZERO_PARAMETER		; It destroys EAX
						CALL	CDC_PUSH_PARAMETERS_BCD

						MOV		DX,IO_CDC_COMMAND
						MOV		AL,CDCCMD_GET_STATE+CDCCMD_STATUSREQ
						OUT		DX,AL

CDDA_GET_STATUS_LOOP:
						CALL	CDC_GET_STATUS
						JC		CDDA_GET_STATUS_LOOP

						OR		AL,AL
						JNE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT

						CMP		AX,0900H
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT
						CMP		AX,0800H
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT

						; CMP		AH,01H	; CDDA is paused?   Should I take it as playing?
						; JE		CDDA_GET_STATUS_PLAYING

						CMP		AH,03H	; CDDA is playing?
						JNE		CDDA_GET_STATUS_NOT_PLAYING

CDDA_GET_STATUS_PLAYING:
						MOV		BYTE [BP+SAVED_EAX],1

						; Memo to myself:
						;   See Tsugaru development journal 2020/04/03 for CL=20H case.
						CMP		CL,20H
						JE		INT93H_EXIT_NO_ERROR

						MOV		DS,[BP+SAVED_DS]
						MOV		DI,[BP+SAVED_EDI]

						CALL	CDC_WAIT_READY	; It destroys AX and DX
						CDC_ZERO_PARAMETER		; It destroys EAX
						CALL	CDC_PUSH_PARAMETERS_BCD

						MOV		DX,IO_CDC_COMMAND
						MOV		AL,CDCCMD_SUBQREAD+CDCCMD_STATUSREQ
						OUT		DX,AL

						MOV		CL,0FFh		; Firs time CX is non-zero
CDDA_SUBQ_READ_LOOP:
						CALL	CDC_GET_STATUS
						JC		CDDA_GET_STATUS_LOOP

						OR		AL,AL
						JE		CDDA_SUBQ_READ_LOOP

						MOV		EBX,EAX
						; 00 00 00 00  <- First return no error.
						; 18H xx trkBCD xx
						; 19H trkTimeBCD trkTimeBCD trkTimeBCD
						; 19H xx discTimeBCD discTimeBCD
						; 20H  discTimeBCD xx xx

						CMP		BL,18H
						JE		CDDA_SUBQ_18H
						CMP		BL,19H
						JE		CDDA_SUBQ_19H
						CMP		BL,20H
						JE		CDDA_SUBQ_20H

CDDA_SUBQ_18H:
						SHR		EAX,16
						CALL	BCD2BIN_AL
						MOV		[DI+1],AL
						JMP		CDDA_SUBQ_READ_LOOP

CDDA_SUBQ_19H:
						JCXZ	CDDA_SUBQ_19H_SECOND	; Second time CX is zero
						XOR		CX,CX

; First time
						XCHG	AL,AH
						CALL	BCD2BIN_AL
						MOV		[DI+3],AL

						SHR		EAX,8
						XCHG	AL,AH
						CALL	BCD2BIN_AL
						MOV		[DI+4],AL

						SHR		EAX,8
						XCHG	AL,AH
						CALL	BCD2BIN_AL
						MOV		[DI+5],AL

						JMP		CDDA_SUBQ_READ_LOOP


CDDA_SUBQ_19H_SECOND:
						SHR		EAX,8
						XCHG	AL,AH
						CALL	BCD2BIN_AL
						MOV		[DI+7],AL

						SHR		EAX,8
						XCHG	AL,AH
						CALL	BCD2BIN_AL
						MOV		[DI+8],AL

						JMP		CDDA_SUBQ_READ_LOOP

CDDA_SUBQ_20H:
						XCHG	AL,AH
						CALL	BCD2BIN_AL
						MOV		[DI+9],AL

						JMP		INT93H_EXIT_NO_ERROR



CDDA_GET_STATUS_NOT_PLAYING:
						MOV		BYTE [BP+SAVED_EAX],0
						JMP		INT93H_EXIT_NO_ERROR

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_54H_CDDA_GET_CDINFO:
						CLI

						CALL	CDC_WAIT_READY	; It destroys AX and DX

						CDC_ZERO_PARAMETER		; It destroys EAX
						CALL	CDC_PUSH_PARAMETERS_BCD

						MOV		DX,IO_CDC_COMMAND
						MOV		AL,CDCCMD_TOCREAD+CDCCMD_IRQ+CDCCMD_STATUSREQ
						OUT		DX,AL

						MOV		DS,[BP+SAVED_DS]
						MOV		DI,[BP+SAVED_EDI]
						MOV		BYTE DS:[DI],0

CDC_TOCREAD_LOOP:
						CALL	CDC_GET_STATUS	; This destroys DX
						CMP		AX,0900H	; 00 09 00 00 Drive Not Ready
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT
						CMP		AX,0800H	; Media Changed
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT
						CMP		AL,21H
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT
						AND		AL,AL
						JE		CDC_TOCREAD_LOOP

						CMP		AL,16H
						JE		CDC_TOCREAD_16H
						CMP		AL,17H
						JE		CDC_TOCREAD_17H
						JMP		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT

CDC_TOCREAD_16H:
						MOV		ECX,EAX
						ROR		ECX,8
						JMP		CDC_TOCREAD_LOOP

CDC_TOCREAD_17H:
						CMP		CH,0A0H		; First track number
						JNE		CDC_TOCREAD_17H_NOT_A0H
						MOV		AL,AH
						CALL	BCD2BIN_AL
						MOV		DS:[DI+1],AL
						JMP		CDC_TOCREAD_LOOP
CDC_TOCREAD_17H_NOT_A0H:
						CMP		CH,0A1H		; Last track number
						JNE		CDC_TOCREAD_17H_NOT_A1H
						MOV		AL,AH
						CALL	BCD2BIN_AL
						MOV		DS:[DI+2],AL
						JMP		CDC_TOCREAD_LOOP
CDC_TOCREAD_17H_NOT_A1H:
						CMP		CH,0A2H		; Disc Length
						JNE		CDC_TOCREAD_17H_NOT_A2H
						MOV		ESI,EAX
						MOV		AL,AH
						CALL	BCD2BIN_AL
						MOV		DS:[DI+3],AL
						MOV		EAX,ESI
						SHR		EAX,16
						CALL	BCD2BIN_AL
						MOV		DS:[DI+4],AL
						MOV		EAX,ESI
						SHR		EAX,24
						CALL	BCD2BIN_AL
						MOV		DS:[DI+5],AL
						JMP		CDC_TOCREAD_LOOP
CDC_TOCREAD_17H_NOT_A2H:
						XCHG	EAX,ECX		; Now EAX is 16H status code (already shifted right by 8 bits), ECX is 17H status code
						ROR		EAX,8
						CALL	BCD2BIN_AL
						DEC		AL
						MOV		BL,AL
						ADD		BL,BL
						ADD		BL,AL	; 3 times (tracknum-1).
						MOVZX	BX,BL
						ADD		BX,6	; DS:[DI+6] for the firs track.
						INC		AL		; Make it back to the max track number.
						ROL		EAX,8
						XCHG	EAX,ECX		; Now EAX is 17H status code, ECX is 16H status code except the track number is in BIN.

						MOV		ESI,EAX
						MOV		AL,AH
						CALL	BCD2BIN_AL
						CMP		CL,40H
						JNE		CDC_TOCREAD_17H_NOT_DATATRK

						OR		AL,80H
						OR		BYTE DS:[DI],2
						JMP		CDC_TOCREAD_17H_STORE
CDC_TOCREAD_17H_NOT_DATATRK:
						OR		BYTE DS:[DI],1
CDC_TOCREAD_17H_STORE:
						MOV		DS:[DI+BX],AL

						MOV		EAX,ESI
						ROR		EAX,16
						CALL	BCD2BIN_AL
						MOV		DS:[DI+BX+1],AL

						MOV		EAX,ESI
						ROR		EAX,24
						CALL	BCD2BIN_AL
						MOV		DS:[DI+BX+2],AL

						ROR		ECX,8	; CL is track number.  Already converted from BCD to BIN.
						CMP		CL,DS:[DI+2]
						JNE		CDC_TOCREAD_LOOP

						; Sequence (all hexadecimal)
						; 16 xx A0 xx (ECX=xxA0xx16)
						; 17 TT xx xx (EAX=xxxxTT17)   TT is the first track in BCD
						; 16 xx A1 xx (ECX=xxA1xx16)
						; 17 TT xx xx (EAX=xxxxTT17)   TT is the last track in BCD
						; 16 xx A2 xx (ECX=xxA2xx16)
						; 17 MM SS FF (EAX=FFSSMM17)   MM,SS,FF are min,sec,frm of the total disc in BCD
						; 16 DT TT xx (ECX=xxTTDT16)   DT is 0 for audio track, 40h for data track. TT is the track number
						; 17 MM SS FF (EAX=FFSSMM17)   MM,SS,FF are min,sec,from of the track.

						MOV		BYTE CS:[ERROR_CODE],0
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_55H_CDDA_PAUSE:
						;  What am I supposed to do if CDDA is not playing?

						CLI

						CALL	CDC_WAIT_READY	; It destroys AX and DX

						CDC_ZERO_PARAMETER		; It destroys EAX
						CALL	CDC_PUSH_PARAMETERS_BCD

						MOV		DX,IO_CDC_COMMAND
						MOV		AL,CDCCMD_CDDA_PAUSE+CDCCMD_IRQ+CDCCMD_STATUSREQ
						OUT		DX,AL

CDC_CDDA_PSUE_LOOP:
						CALL	CDC_GET_STATUS	; This destroys DX
						CMP		AX,0900H	; 00 09 00 00 Drive Not Ready
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT
						CMP		AX,0800H	; Media Changed
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT
						CMP		AL,21H
						JE		INT93H_CD_CDC_ERROR_TO_BIOS_ERROR_EXIT
						CMP		AL,12H
						JNE		CDC_TOCREAD_LOOP

						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_56H_CDDA_RESUME:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


; Wait until CDC is ready.
; Destroys AL and DX
CDC_WAIT_READY:
						MOV		DX,IO_CDC_MASTER_STATUS
						IN		AL,DX
						AND		AL,1
						JE		CDC_WAIT_READY
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Push parameters to CDC
CDC_PUSH_PARAMETERS:
						PUSH	BX

						XOR		BX,BX
						MOV		DX,IO_CDC_PARAM

CDC_PUSH_PARAMETERS_LOOP:
						MOV		AL,CS:[CDC_PARAM+BX]
						OUT		DX,AL
						INC		BX
						CMP		BX,8
						JB		CDC_PUSH_PARAMETERS_LOOP

						POP		BX
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Push parameters to CDC
CDC_PUSH_PARAMETERS_BCD:
						PUSH	BX

						XOR		BX,BX
						MOV		DX,IO_CDC_PARAM

CDC_PUSH_PARAMETERS_BCD_LOOP:
						MOV		AL,CS:[CDC_PARAM+BX]
						CALL	BIN2BCD_AL
						OUT		DX,AL
						INC		BX
						CMP		BX,8
						JB		CDC_PUSH_PARAMETERS_BCD_LOOP

						POP		BX
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CDC_GET_STATUS:
						MOV		DX,IO_CDC_MASTER_STATUS
						IN		AL,DX
						AND		AL,02H
						JE		CDC_GET_STATUS_NO_STATUS

						MOV		DX,IO_CDC_STATUS
						IN		AL,DX
						ROR		EAX,8
						IN		AL,DX
						ROR		EAX,8
						IN		AL,DX
						ROR		EAX,8
						IN		AL,DX
						ROR		EAX,8

						CLC

						RET

CDC_GET_STATUS_NO_STATUS:
						XOR		EAX,EAX
						STC
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CDC_SETUP_DMA_FOR_ONE_SECTOR:
						; Set up DMA

						MOV		AL,3	; Reset SCSI Controller
						OUT		IO_DMA_INITIALIZE,AL
						MOV		AL,3	; Channel 3 CD-ROM
						OUT		IO_DMA_CHANNEL,AL
						MOV		AL,20H	; DMA enable
						OUT		IO_DMA_DEVICE_CTRL_LOW,AL

						MOV		AL,44H	; Device to Mem
						OUT		IO_DMA_MODE_CONTROL,AL

						MOV		EAX,CS:[CDC_READ_POINTER_PHYS]
						OUT		IO_DMA_ADDR_LOW,AX
						SHR		EAX,16
						OUT		IO_DMA_ADDR_MID_HIGH,AL
						MOV		AL,AH
						OUT		IO_DMA_ADDR_HIGH,AL

						MOV		AX,0800H	; Up to 2KB for one sector
						OUT		IO_DMA_COUNT_LOW,AX

						; Unmask DMA
						IN		AL,IO_DMA_MASK
						AND		AL,07H
						OUT		IO_DMA_MASK,AL

						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

CDC_WAIT_DMAE:
						MOV		DX,IO_CDC_MASTER_STATUS
						IN		AL,DX
						AND		AL,40H
						JE		CDC_WAIT_DMAE
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_ENABLE_EMULATION:
						; Identify SCSI device type of ID in DL
						; If successful jump INT93H_CD_ENABLE_EMULATION_SUCCESSFUL

						MOV		BYTE CS:[ERROR_CODE],0

						CMP		DL,0FFh
						JNE		INT93H_CD_ENABLE_EMULATION_NOT_FFH

						MOV		DL,CS:[CD_EMULATION_SCSI_DEV]
						AND		DL,7

INT93H_CD_ENABLE_EMULATION_NOT_FFH:
						PUSH	DX
						; Input
						;   CL   SCSI ID, or 0FFh for do not change the SCSI ID.
						; Output
						;   CL   05 or 04 means CD
						;        00 means HD
						;   Carry  Set if error
						MOV		CL,DL
						CALL	IDENTIFY_SCSI_DEVICE
						POP		DX

						JC		INT93H_CD_ENABLE_EMULATION_ERROR

						;	CL needs to be 4 or 5.  unsigned(CL-4)<=1
						SUB		CL,4
						CMP		CL,1
						JA		INT93H_CD_ENABLE_EMULATION_ERROR

						; SCSI ID DL is connected and a CD-ROM or CD-R.

						AND		DL,0FH;
						OR		DL,DISKBIOS_SCSI_0
						MOV		CS:[CD_EMULATION_SCSI_DEV],DL

						MOV		BYTE CS:[CD_EMULATION_SCSI_EN],1

						JMP		INT93H_EXIT

INT93H_CD_ENABLE_EMULATION_ERROR:
						MOV		BYTE CS:[ERROR_CODE],1
						JMP		INT93H_EXIT


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_DISABLE_EMULATION:
						MOV		BYTE CS:[CD_EMULATION_SCSI_EN],0
						MOV		BYTE CS:[ERROR_CODE],0
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_02H_GET_DRVSTATUS: ; It says Drive Status, but is rather Disc status.
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_03H_RESTORE:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_04H_SEEK_LBA:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_05H_READ_LBA:
						MOV		CH,[BP+SAVED_ECX+1]
						CMP		CH,0FFH
						JE		INT93H_CD_EMU_05H_READ_LBA_SPECIAL

						MOVZX	EAX,WORD [BP+SAVED_DS]
						SHL		EAX,4
						MOVZX	EDI,WORD [BP+SAVED_EDI]
						ADD		EDI,EAX

						JMP		INT93H_CD_EMU_05H_READ_LBA_CONTINUE

INT93H_CD_EMU_05H_READ_LBA_SPECIAL:
						; Special mode SI|DI is the physical address.
						MOV		DI,WORD [BP+SAVED_ESI]
						SHL		EDI,16
						MOV		DI,WORD [BP+SAVED_EDI]

INT93H_CD_EMU_05H_READ_LBA_CONTINUE:
						MOV		CL,CS:[CD_EMULATION_SCSI_DEV]
						AND		CL,7

						MOV		DX,[BP+SAVED_ECX]
						SHL		EDX,16
						MOV		DX,[BP+SAVED_EDX]

						MOV		BX,[BP+SAVED_EBX]

						PUSH	CS
						POP		DS

						MOV		BYTE CS:[ERROR_CODE],0

						; Input
						;   CL    SCSI ID
						;   EDX   Starting Sector
						;   BX    Number of Sectors
						;   EDI   Data Buffer Physical Address
						;   DS=CS
						; Output
						;   CF    Set if error
						CALL	SCSI_READ_SECTOR
						JNC		INT93H_EXIT

						MOV		BYTE CS:[DISC_CHANGE_FLAG_CACHE],1
						MOV		BYTE CS:[ERROR_CODE],80h
						MOV		BYTE CS:[ERROR_DETAIL],1
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_0FH_CHECK_DISC_CHANGE:
						XOR		DL,DL
						XCHG	DL,CS:[DISC_CHANGE_FLAG_CACHE]	; Load DISC_CHANGE_FLAG_CACHE to DL, and clear DISC_CHANGE_FLAG_CACHE
						MOV		[BP+SAVED_EDX],DL				; Tentative.

						MOV		BYTE CS:[ERROR_CODE],00H

						MOV		CL,CS:[CD_EMULATION_SCSI_DEV]
						AND		CL,7
						CALL	SCSI_TEST_UNIT_READY

						AND		AH,AH
						JE		INT93H_EXIT

						MOV		CL,CS:[CD_EMULATION_SCSI_DEV]
						AND		CL,7
						CALL	SCSI_SENSE

						; Take any error as drive not ready.
						MOV		BYTE [BP+SAVED_EDX],1
						MOV		BYTE CS:[ERROR_CODE],80H
						MOV		WORD CS:[ERROR_DETAIL],1

						JMP		INT93H_EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_14H_SEEK_MSF:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_15H_READ_MSF:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_50H_CDDA_PLAY:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_51H_CDDA_GET_INFO:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_52H_CDDA_STOP:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_53H_CDDA_GET_STATUS:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_54H_CDDA_GET_CDINFO:
						TSUGARU_DEBUG

						MOV		CL,CS:[CD_EMULATION_SCSI_DEV]
						AND		CL,7
						CALL	SCSI_READ_SUB_CHANNEL_CDROM_CUR_POS
						JNC		SHORT INT93H_CD_EMU_54H_NOT_HARD_ERROR
						MOV		BYTE CS:[ERROR_CODE],80H	; Tentative Hard Error
						JMP		INT93H_EXIT

INT93H_CD_EMU_54H_NOT_HARD_ERROR:
						MOV		AL,CS:[SCSI_DATABUF+1]
						CMP		AL,11H ; 11H=PLAYING
						JNE		SHORT INT93H_CD_EMU_54H_NOT_ALREADY_PLAYING
						MOV		BYTE CS:[ERROR_CODE],DISKBIOS_ERROR_CD_PLAYING
						JMP		INT93H_EXIT

INT93H_CD_EMU_54H_NOT_ALREADY_PLAYING:
						MOV		CL,CS:[CD_EMULATION_SCSI_DEV]
						MOV		CH,0AAH		; Get min-max track
						CALL	SCSI_READTOC
						JNC		INT93H_CD_EMU_54H_HAVE_MIN_MAX_TRACK
						MOV		BYTE CS:[ERROR_CODE],80H	; Tentative Hard Error
						JMP		INT93H_EXIT

INT93H_CD_EMU_54H_HAVE_MIN_MAX_TRACK:
						MOV		CL,CS:[SCSI_DATABUF+2]	; First track
						MOV		CH,CS:[SCSI_DATABUF+3]	; Last track

						MOV		ES,[BP+SAVED_DS]
						MOV		SI,[BP+SAVED_EDI]

						MOV		BYTE ES:[SI],0		; Tentatively no data/no audio
						MOV		ES:[SI+1],CX			; First/Last track


						; Volume size is also obtained from RedTOC in CDSD.SYS
						; VolSize calls DskInfo, which shoots RedTOC with CX=0,
						; RedTOC issues SCSI call READ TOC with Track=0AAH.
						; BL(Min), DH(Sec), DL(Frm) are taken from data buffer+9, 10, 11
						; Min, Sec, Frm are then converted to HSG and returned.

						MOV		DL,CS:[SCSI_DATABUF+9]
						MOV		AX,CS:[SCSI_DATABUF+10]
						; !CAUTION!  SCSI uses big-endian.  AL is HIBYTE(sec) and AH is LOBYTE(frm).

						; For unknown reason, CDSD.SYS seems to subtract 150 frames (2 seconds) from volume size.
						; Uncomment the following if it is correct.
						; But, to make it compatible with FM TOWNS CD-ROM BIOS, it shouldn't subtract 2 seconds apparently.
;						SUB		AL,2	; 2 seconds
;						JNC		SHORT @f
;						ADD		AL,60
;						DEC		DL
;@@:
						MOV		ES:[SI+3],DL
						MOV		ES:[SI+4],AX

						LEA		DI,[SI+6]
						MOV		DL,ES:[SI+1]

INT93H_CD_EMU_54H_TRACK_LOOP:
						PUSH	DI
						PUSH	DX

						MOV		CH,DL
						MOV		CL,CS:[CD_EMULATION_SCSI_DEV]
						AND		CL,7
						PUSH	ES
						PUSH	SI
						CALL	SCSI_READTOC
						POP		SI
						POP		ES
						JNC		SHORT INT93H_CD_EMU_54H_READTOC_NOERR

						POP		DX
						POP		DI
						MOV		BYTE CS:[ERROR_CODE],80H	; Tentative Hard Error
						JMP		INT93H_EXIT

INT93H_CD_EMU_54H_READTOC_NOERR:
						;[Summary]
						;  [SCSI_DATABUF+5]==14H indicates it is a data track, audio track otherwise.
						;[Details]
						; From assembly code of CDSD.SYS
						; Indication of Data Track 41H is coming from 5th byte of data buffer in RedTOC:
						;	mov	cx,[si+5]		; Set ADR/Control
						;	xchg	ch,cl			; ch = ADR/CTL, cl = Track Address
						; And then rolled 4 times in AudTrk:
						;	rol	ch,1
						;	rol	ch,1
						;	rol	ch,1
						;	rol	ch,1
						; Which is copied to IOCTL communicatoin buffer +6:
						;	mov	es:[di+6],ch		; Control Information
						; Then CDISC2.SYS receives it in GetCDInfo:
						;		mov	ax, word ptr es:[si.I_CtrlInfo]
						;
						; Actual read from a CD data track (track 1):
						;+0+1+2+3+4+5
						;00520109001401000000020000000000
						;          ^^
						; Actual read from a CD audio track (track 2):
						;+0+1+2+3+4+5
						;004A0109001002000028060200000000
						;
						;Fifth byte of Read TOC (43H) return is described as:
						; ADR | Control
						; What's ADR and Control?  ADR is the type of information encoded in the Q sub-channel.  Control is the atribute of the track.
						;
						; According to https://www.staff.uni-mainz.de/tacke/scsi/SCSI2-14.html, Control bits are:
						;   Bit 0: Audio has pre-emphasis
						;   Bit 1: Digital copy permitted
						;   Bit 2: Is data track
						;   Bit 3: Four-channel audio
						;
						; Probably MSCDEX's ADR/Control byte has ADR and Control swapped from SCSI2 spec.

						POP		DX
						POP		DI

						MOV		AH,1  ; Tentatively audio
						MOV		AL,BYTE CS:[SCSI_DATABUF+9]
						CMP		BYTE CS:[SCSI_DATABUF+5],14H
						JNE		SHORT INT93H_CD_EMU_54H_IS_AUDIO_TRACK
						MOV		AH,2	; Data track
						OR		AL,80H
INT93H_CD_EMU_54H_IS_AUDIO_TRACK:
						OR		ES:[SI],AH

						MOV		ES:[DI],AL
						MOV		AX,WORD CS:[SCSI_DATABUF+10]
						MOV		ES:[DI+1],AX

						LEA		DI,[DI+3]

						INC		DL
						CMP		DL,ES:[SI+2]
						JBE		SHORT INT93H_CD_EMU_54H_TRACK_LOOP

						JMP		INT93H_EXIT_NO_ERROR

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_55H_CDDA_PAUSE:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_CD_EMU_56H_CDDA_RESUME:
						TSUGARU_DEBUG
						JMP		INT93H_EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; HSG=60*MIN+75*SEC+FRM  ->  60 seconds/min,   75 frames/sec   -> 60*75 frames/min
; Input:
;	DX:AX	HSG
; Output:
;	DL		MIN
;	AH		SEC
;	AL		FRM
HSG2MSF:
						PUSH	CX

						MOV		CX,60*75
						DIV		CX
						; AX minutes, DX sub-minute

						XOR		AH,AH
						PUSH	AX		; MIN in stack

						MOV		AX,DX
						XOR		DX,DX
						MOV		CX,75
						DIV		CX
						; AL seconds, DL frames

						MOV		AH,AL
						MOV		AL,DL

						POP		DX
						POP		CX
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; HSG=60*MIN+75*SEC+FRM  ->  60 seconds/min,   75 frames/sec   -> 60*75 frames/min
; Input:
;	DL		MIN
;	AH		SEC
;	AL		FRM
; Output:
;	DH		0
;	DL		High-byte of HSG
;	AH		Mid-byte of HSG
;	AL		Low-byte of HSG

MSF2HSG:
						PUSH	CX
						PUSH	BX

						XOR		DH,DH
						; DX=MIN
						PUSH	DX

						MOV		CL,AL
						XOR		CH,CH
						; CX=FRM

						MOV		AL,AH
						XOR		AH,AH
						; AX=SEC

						MOV		BX,75
						MUL		BX
						; AX=SEC*75

						ADD		CX,AX
						; CX=SEC*75+FRM

						POP		DX
						; DX=MIN
						MOV		AX,60*75
						MUL		DX

						ADD		AX,CX
						ADC		DX,0

						POP		BX
						POP		CX
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

						%INCLUDE "LIB.NSM"
