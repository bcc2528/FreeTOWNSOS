; Fake FORRBIOS.NSD + CoCo memory manager
; It reacts to:
; INT 8EH
;   AX=C000H Installation Check
;     Output
;       CX=436FH DX=436FH SI=204Bh DI=656EH "CoCo Ken"  BH=32H

;   AX=C10CH Register Call Buffer
;     Input   DX=Call-Buffer DOSSEG
;             If buffer size>=16KB  CX=Buffer Size divided by 16KB, ESI=0FFFF0000H  Buffer size needs to be 16KB*N
;             Else                  CX=1  ESI=Only low xx bits are 0, where the buffer size is xxKB.

;   AX=C207H Register Call32 Proc
;     Input DX:BX=CS:IP

;   AX=C103H
;     Input  CX=Driver Number
;     Output 
;       AH=0 Driver exists  AH=non-zero Driver does not exist
;       CX=Number of drivers
;       BX=CS selector of the driver
;       DX=DS selector of the driver
;       DS:EDI=8-byte two LDT entries for CS and DS  CS first, DS next
;     Sample output:
;       EBX=0000004C  EDX=00000044
;       DS:0000E790 FF 7F 00 50 24 9A 40 00 FF 7F 00 50 24 92 40 00
;         Then,
;       Code 004C:LiBase=00245000 Lim=00007FFF OpSz=20H AdSz=20H P=01 DPL=00 Type=1AH @ PHYS:0003ED28H
;       Data 0044:LiBase=00245000 Lim=00007FFF OpSz=20H AdSz=20H P=01 DPL=00 Type=12H @ PHYS:0003ED20H
;         WORD DS:[6] will be Strategy entry, which receives DS:EBX as the command header
;         WORD DS:[8] will be Interrupt entry.
;       Inherits the DOS Device-Driver structure.

; It pretends to have only one NSDD, FORRBIOS.NSD.


						CPU		386
						BITS	16

						%INCLUDE "UTIL.NSM"
						%INCLUDE "DEF.NSM"
						%INCLUDE "TOWNSDEF.NSM"


						DD      0FFFFFFFFh                  ; SYSDEV_NEXT
						DW      SYSDEV_DEVFLAG_IS_CHARDEV   ; SYSDEV_DEVFLAGS
STRATEGY_PTR			DW      STRATEGY16                  ; SYSDEV_STRATEGY
INTERRUPT_PTR			DW      INTERRUPT16                 ; SYSDEV_INTERRUPT
DRIVER_NAME				DB      "********"

CASCADE_INT8E			DD		0
REQ_HEADER				DD		0
REQ_HEADER_SEG			DW		0

CALLBUF_PTR				DD		0
CALLBUF_SIZE			DD		0

CALL32PROC				DD		0

PROT_SELECTOR_CS		EQU		4CH
PROT_SELECTOR_DS		EQU		44H




;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

						BITS	32

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 32-bit Process ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

STRATEGY32:
						PUSH	FS
						PUSH	PROT_SELECTOR_DS
						POP		FS

						MOV		FS:[REQ_HEADER_SEG],DS
						MOV		FS:[REQ_HEADER],EBX

						POP		FS
						RETF

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 32-bit Process ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INTERRUPT32:
						SAVE_WORLD

						LFS		EAX,CS:[REQ_HEADER]
						MOV		AH,FS:[EAX+2]
						; 0C1H	; Wake Up
						; 0C0H	; Sleep

						CMP		AH,0C1H
						JE		FORRBIOS_WAKEUP
						CMP		AH,0C0H
						JE		FORRBIOS_SUTDOWN

INTERRUPT32_EXIT:
						RESTORE_WORLD

						; I probably need to set NO_ERROR state in somewhere, but there is no information about what to write to where.

						RETF

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 32-bit Process ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

ORIGINAL_INT93H_HANDLER			DD	0
ORIGINAL_INT93H_HANDLER_SEG		DD	0

ORIGINAL_INT97H_HANDLER			DD	0
ORIGINAL_INT97H_HANDLER_SEG		DD	0

FORRBIOS_WAKEUP:
						MOV		AX,PROT_SELECTOR_DS
						MOV		DS,AX

						MOV		AX,2502H	; Get Protected-Mode INT Vector
						MOV		CL,93H
						INT		21H
						MOV		[ORIGINAL_INT93H_HANDLER],EBX
						MOV		[ORIGINAL_INT93H_HANDLER_SEG],ES

						MOV		AX,2502H	; Get Protected-Mode INT Vector
						MOV		CL,97H
						INT		21H
						MOV		[ORIGINAL_INT97H_HANDLER],EBX
						MOV		[ORIGINAL_INT97H_HANDLER_SEG],ES

						MOV		AX,2504H
						MOV		CL,93H
						PUSH	CS
						POP		DS
						MOV		EDX,INT93H_INTERCEPT
						INT		21H

						MOV		AX,2504H
						MOV		CL,97H
						PUSH	CS
						POP		DS
						MOV		EDX,INT97H_INTERCEPT
						INT		21H

						JMP		INTERRUPT32_EXIT


FORRBIOS_SUTDOWN:
						JMP		INTERRUPT32_EXIT




;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 32-bit Process ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_INTERCEPT:
						SAVE_WORLD

						CMP		AX,50C0H
						JE		INT93H_50C0H
						CMP		AX,53C0H
						JE		INT93H_53C0H
						CMP		AX,54C0H
						JE		INT93H_54C0H

						; Not by FORRBIOS.  Just jump to the default INT handler.
						; CALL will make a problem because the default handler identifes the INTNum by looking at the return CS:EIP-1, but JMP is ok.
						RESTORE_WORLD
						JMP		FAR [CS:ORIGINAL_INT93H_HANDLER]


; Returned CF, AX, and CX must be preserved.
; High-C CDRFRB functions returns CX if AH is 80h.  Therefore, CX must be correctly transferred back.
INT93H_INTERCEPT_RETURN:
						MOV		[ESP+SAVED_EAX32+1],AH
						JC		INT93H_INTERCEPT_RETURN_ERR
						AND		BYTE [ESP+RETURN_32FLAGS],0FEH
						JMP		INT93H_INTERCEPT_RETURN_IRET


INT93H_INTERCEPT_RETURN_ERR:
						OR		BYTE [ESP+RETURN_32FLAGS],1

						CMP		AH,80H
						JNE		INT93H_INTERCEPT_RETURN_IRET

						MOVZX	ECX,CX
						MOV		[ESP+SAVED_ECX32],ECX


INT93H_INTERCEPT_RETURN_IRET:
						RESTORE_WORLD
						IRET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_50C0H:
						MOV		BX,DS:[EDI]
						MOV		CX,DS:[EDI+2]
						MOV		DX,DS:[EDI+4]
						MOV		AH,71H	; CD BIOS Extension.  Play CDDA with regsiter interface.
						INT		93H

						; CF,AX,CX needs to be preserved.

						JMP		INT93H_INTERCEPT_RETURN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_53C0H:
						MOV		EDI,CS:[CALLBUF_PTR]	; EDI=DX|DI in the real mode.
						ADD		AH,30H					; Make it 83H

						INT		93H						; Need to make a recursive call because Free386 identifies INTNum by looking at the return CS:EIP-1
						JC		INT93H_INTERCEPT_RETURN

						MOV		EBP,ESP

						PUSHFD	; CF,AX,CX needs to be preserved.

						MOV		[EBP+SAVED_EAX32],AL		; Playing or Not playing

						CLD

						MOV		ESI,CS:[CALLBUF_PTR]
						XOR		SI,SI
						SHR		ESI,12
						MOVZX	EDI,WORD CS:[CALLBUF_PTR]
						ADD		ESI,EDI
						MOV		DI,SELECTOR_DOSMEM
						MOV		DS,DI

						MOV		EDI,[EBP+SAVED_EDI32]
						MOV		ES,[EBP+SAVED_DS32]

						MOVSD
						MOVSD
						MOVSW

						POPFD  	; CF,AX,CX needs to be preserved.

						JMP		INT93H_INTERCEPT_RETURN


INT93H_54C0H:
						MOV		EDI,CS:[CALLBUF_PTR]	; EDI=DX|DI in the real mode.
						ADD		AH,30H					; Make it 83H

						INT		93H						; Need to make a recursive call because Free386 identifies INTNum by looking at the return CS:EIP-1

						MOV		EBP,ESP

						PUSHFD	; CF,AX,CX needs to be preserved.
						PUSH	ECX

						AND		AH,AH
						JNE		INT93H_54C0H_ERROR

						CLD

						MOV		ESI,CS:[CALLBUF_PTR]
						XOR		SI,SI
						SHR		ESI,12
						MOVZX	EDI,WORD CS:[CALLBUF_PTR]
						ADD		ESI,EDI
						MOV		DI,SELECTOR_DOSMEM
						MOV		DS,DI

						MOV		EDI,[EBP+SAVED_EDI32]
						MOV		ES,[EBP+SAVED_DS32]

						MOV		DL,DS:[ESI+2]
						INC		DL
						SUB		DL,DS:[ESI+1]

						MOVZX	EDX,DL
						LEA		ECX,[EDX+EDX*2+6]

						REP		MOVSB

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT93H_54C0H_ERROR:
						POP		ECX
						POPFD	; CF,AX,CX needs to be preserved.

						JMP		INT93H_INTERCEPT_RETURN


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 32-bit Process ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

INT97H_INTERCEPT:
						AND		AH,AH
						JE		INT97H_00H_REGISTER_TIMER

						JMP		FAR [CS:ORIGINAL_INT97H_HANDLER]

; 32-bit version of register timer
;  DS:EDI    BYTE  Timer Mode
;  DS:EDI+1  BYTE  Unused
;  DS:EDI+2  DWORD EIP of the callback
;  DS:EDI+6  WORD  CS of the callback
;  DS:EDI+8  WORD  Timer Interval High
;  DS:EDI+10 WORD  Timer Interval Low
INT97H_00H_REGISTER_TIMER:
						SAVE_WORLD

						MOV		AX,[EDI+6]
						SHL		EAX,16
						MOV		AH,TIMERBIOS_CMD_070H_YS_REGISTER_TIMER32
						MOV		AL,[EDI]

						MOV		EBX,[EDI+2]

						MOV		ECX,[EDI+8]
						ROL		ECX,16

						MOV		EDX,CS:[CALL32PROC]

						MOV		SI,DS
						SHL		ESI,16
						MOV		SI,ES

						MOV		DI,FS
						SHL		EDI,16
						MOV		DI,GS

						; INT 97H Extension
						; AH=70H
						; AL=Timer Mode
						; (HIWORD EAX):EBX=Protected-mode INT proc.
						; ECX=Interval
						; EDX=Real-Mode Call Back Proc CS|IP
						; ESI=ES|DS
						; EDI=GS|FS

						INT		97H
						; Free386 checks INT Num by CS:[EIP-1] where CS:EIP is return addr.
						; Only way to correctly call INT 97H is recursive INT.

						RESTORE_WORLD

						IRET


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

						BITS	16
INT8E_INTERCEPT:
						CMP		AX,0C000H
						JE		INT8E_C000_INST_CHECK
						CMP		AX,0C10CH
						JE		INT8E_C10C_REGIST_CALLBUF
						CMP		AX,0C207H
						JE		INT8E_C207_REGIST_CALL32PROC
						CMP		AX,0C103H
						JE		INT8E_C103_GET_NSD

						CMP		AX,0CF00H
						JE		INT8E_CF00_UNKNOWN
						CMP		AX,0C109H
						JE		INT8E_C109_UNKNOWN

						JMP		FAR [CS:CASCADE_INT8E]


; VIPS2 uses these two functions.  Meaning is unknown.  Just pretend to be ok.
INT8E_CF00_UNKNOWN:
INT8E_C109_UNKNOWN:
						; Clear CF
						PUSH	BP
						MOV		BP,SP	; [BP+0]=BP  [BP+2]=ReturnIP  [BP+4]=ReturnCS  [BP+6]=FLAGS
						AND		BYTE [BP+6],0FEH
						POP		BP
						XOR		AH,AH
						IRET



;   AX=C000H Installation Check
;     Output
;       AH=0  CX=436FH DX=436FH SI=204Bh DI=656EH "CoCo Ken"  BH==' '  BL=questionable
INT8E_C000_INST_CHECK:
						XOR		AH,AH
						MOV		CX,436FH
						MOV		DX,436FH
						MOV		SI,204Bh
						MOV		DI,656EH
						MOV		BH,32H

						; Clear CF
						PUSH	BP
						MOV		BP,SP	; [BP+0]=BP  [BP+2]=ReturnIP  [BP+4]=ReturnCS  [BP+6]=FLAGS
						AND		BYTE [BP+6],0FEH
						POP		BP
						IRET



;   AX=C10CH Register Call Buffer
;     Input   DX=Call-Buffer DOSSEG
;             If buffer size>=16KB  CX=Buffer Size divided by 16KB, ESI=0FFFF0000H  Buffer size needs to be 16KB*N
;             Else                  CX=1  ESI=Only low xx bits are 0, where the buffer size is xxKB.
INT8E_C10C_REGIST_CALLBUF:
						TSUGARU_STATE

						SAVE_WORLD

						MOV		CS:[CALLBUF_PTR+2],DX

						MOVZX	ECX,CX
						OR		ESI,0FFFF0000H

						JCXZ	INT8E_C10C_REGIST_CALLBUF_EXIT
						CMP		CX,1
						JNE		INT8E_C10C_REGIST_CALLBUF_SIMPLE

						; Less than 16KB
						BSF		ECX,ESI
						SHL		ECX,10
						JMP		INT8E_C10C_REGIST_CALLBUF_EXIT

INT8E_C10C_REGIST_CALLBUF_SIMPLE:
						SHL		ECX,4+10	; CX=size/16KB

INT8E_C10C_REGIST_CALLBUF_EXIT:
						MOV		CS:[CALLBUF_SIZE],ECX

						RESTORE_WORLD
						XOR		AH,AH
						IRET



;   AX=C207H Register Call32 Proc
;     Input DX:BX=CS:IP
INT8E_C207_REGIST_CALL32PROC:
						TSUGARU_STATE
						MOV		CS:[CALL32PROC],BX
						MOV		CS:[CALL32PROC+2],DX
						XOR		AH,AH
						IRET



;   AX=C103H
;     Input  CX=Driver Number
;     Output 
;       AH=0 Driver exists  AH=non-zero Driver does not exist
;       CX=Number of drivers
;       BX=CS selector of the driver
;       DX=DS selector of the driver
;       DS:EDI=8-byte two LDT entries for CS and DS  CS first, DS next
;     Sample output:
;       EBX=0000004C  EDX=00000044
;       DS:0000E790 FF 7F 00 50 24 9A 40 00 FF 7F 00 50 24 92 40 00
;         Then,
;       Code 004C:LiBase=00245000 Lim=00007FFF OpSz=20H AdSz=20H P=01 DPL=00 Type=1AH @ PHYS:0003ED28H
;       Data 0044:LiBase=00245000 Lim=00007FFF OpSz=20H AdSz=20H P=01 DPL=00 Type=12H @ PHYS:0003ED20H
;         WORD DS:[6] will be Strategy entry, which receives DS:EBX as the command header
;         WORD DS:[8] will be Interrupt entry.
;       Inherits the DOS Device-Driver structure.
INT8E_C103_GET_NSD:
						TSUGARU_STATE
						JCXZ	INT8E_C103_GET_NSD_ZERO

INT8E_C103_GET_NSD_NOT_ZERO:
						MOV		AH,0FFH ; Return AH=non_zero to tell drive ID=CX does not exist.
						PUSH	BP
						MOV		BP,SP
						OR		BYTE [BP+6],1
						POP		BP
						IRET

INT8E_C103_GET_NSD_ZERO:
						PUSH	EAX

						MOV		CX,1
						MOV		BX,PROT_SELECTOR_CS
						MOV		DX,PROT_SELECTOR_DS

						MOV		AX,CS
						MOVZX	EAX,AX
						SHL		EAX,4
						MOV		[DI+2],EAX

						MOV		WORD [DI],END_OF_FAKENSDD_DRIVER
						MOV		BYTE [DI+5],9AH	; Present, DPL=0, Type=1AH
						MOV		BYTE [DI+6],40H	; Granularity=bytes, 32-bit operand size, Limit upper byte=0
						MOV		BYTE [DI+7],0

						MOV		WORD [DI+8],END_OF_FAKENSDD_DRIVER
						MOV		[DI+10],EAX
						MOV		BYTE [DI+13],92H	; Present, DPL=0, Type=12H
						MOV		BYTE [DI+14],40H	; Granularity=bytes, 32-bit operand size, Limit upper byte=0
						MOV		BYTE [DI+15],0

						POP		EAX

						XOR		AH,AH	; Should I clear CF?
						PUSH	BP
						MOV		BP,SP
						AND		BYTE [BP+6],0FEH
						POP		BP
						IRET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
FAKENSDD_LOGO:			DB		"FAKE FORRBIOS.NSD Driver by CaptainYS (http://www.ysflight.com)",0Dh,0Ah,'$',0

END_OF_FAKENSDD_DRIVER:

STRATEGY16:
						MOV		CS:[REQ_HEADER],BX
						MOV		CS:[REQ_HEADER+2],ES
						RETF

INTERRUPT16:
						SAVE_WORLD

						LES		BX,CS:[REQ_HEADER]

						MOV		WORD ES:[BX+REQ_STATUS],DEVREQ_STATUS_ERROR

						MOV		AL,ES:[BX+REQ_COMMAND]
						CMP		AL,DEVREQ_CMD_INIT
						JNE		INTERRUPT_EXIT

						MOV		WORD ES:[BX+REQ_STATUS],DEVREQ_STATUS_NOERROR

						XOR		AX,AX
						MOV		DS,AX

						MOV		EAX,[4*08EH]
						MOV		CS:[CASCADE_INT8E],EAX

						MOV		AX,CS
						SHL		EAX,16
						MOV		AX,INT8E_INTERCEPT
						MOV		[4*08EH],EAX

						MOV		AX,STRATEGY32
						MOV		CS:[STRATEGY_PTR],AX
						MOV		AX,INTERRUPT32
						MOV		CS:[INTERRUPT_PTR],AX

						MOV		WORD ES:[BX+REQ_INIT_END_OF_RESIDENT_CODE_RETURN],END_OF_FAKENSDD_DRIVER
						MOV		ES:[BX+REQ_INIT_END_OF_RESIDENT_CODE_RETURN+2],CS

INTERRUPT_EXIT:
						RESTORE_WORLD
						RETF

END_OF_MINVCPI:			DB		"FM-series forever! (Excluding FM-V.  I don't care FM-V.)",0
