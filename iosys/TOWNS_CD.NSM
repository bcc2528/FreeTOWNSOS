TOWNSCD_HEADER_NEXT				DW	BLKDEV_HEADER_NEXT
								DW	IOSYS_CS
TOWNSCD_HEADER_ATTRIBWORD		DW	0C800h  ; CharDev|IOCTL|open/close/removable media
TOWNSCD_HEADER_STRATEGY_PTR		DW	TOWNSCD_STRATEGY
TOWNSCD_HEADER_INTERRUPT_PTR	DW	TOWNSCD_INTERRUPT
TOWNSCD_HEADER_NAME				DB	"TOWNS_CD"

								DB	0 ; Offset +12H (18)
								DB 	0 ; Offset +13H (19)
								DB	0 ; Offset +14H (20)
								DB	1 ; Offset +15H (21) SHSCDX expects number of units.

TOWNSCD_REQHDR_PTR		DD	0


MSCDEX_ERROR_UNKNOWN_CMD		EQU		8003H

MSCDEX_MEDIA_CHANGED			EQU		0FFH
MSCDEX_MEDIA_CHANGE_UNKNOWN		EQU		0
MSCDEX_MEDIA_DID_NOT_CHANGE		EQU		1


TOWNSCD_STRATEGY:
						MOV		CS:[TOWNSCD_REQHDR_PTR],BX
						MOV		CS:[TOWNSCD_REQHDR_PTR+2],ES
						RETF

TOWNSCD_INTERRUPT:
						SAVE_WORLD

						LES		BX,CS:[TOWNSCD_REQHDR_PTR]
						MOV		WORD ES:[BX+REQ_STATUS],DEVREQ_STATUS_NOERROR ; Tentatively no error
						MOV		AL,ES:[BX+REQ_COMMAND]

						CMP		AL,DEVREQ_CMD_INIT
						JE		TOWNSCD_INIT
						CMP		AL,DEVREQ_CMD_IOCTL_READ
						JE		TOWNSCD_IOCTL_READ
						CMP		AL,DEVREQ_CMD_OPEN
						JE		TOWNSCD_IOCTL_OPEN
						CMP		AL,DEVREQ_CMD_CLOSE
						JE		TOWNSCD_IOCTL_CLOSE

						CMP		AL,80H
						JE		TOWNSCD_READ_SECTORS
						CMP		AL,82H
						JE		TOWNSCD_NAZO_FUNCTION_82H

						CMP		AL,DEVREQ_CMD_WRITE				;		08h
						JE		TOWNSCD_WRITE
						CMP		AL,DEVREQ_CMD_WRITE_VERIFY			;		09h
						JE		TOWNSCD_WRITE

						CMP		AL,DEVREQ_CMD_OPEN					;		0Dh
						JE		TOWNSCD_OPEN
						CMP		AL,DEVREQ_CMD_CLOSE				;		0Eh
						JE		TOWNSCD_CLOSE

						TSUGARU_DEBUG

TOWNSCD_INTERRUPT_EXIT:
						RESTORE_WORLD
						RETF

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

TOWNSCD_INIT:
TOWNSCD_OPEN:
TOWNSCD_CLOSE:
TOWNSCD_IOCTL_OPEN:
TOWNSCD_IOCTL_CLOSE:
						JMP		TOWNSCD_INTERRUPT_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

TOWNSCD_MSG_ENABLED_REDIR			DB	"Enabled SCSI CD Redirection SCSI ID=",
TOWNSCD_MSG_ENABLED_REDIR_SCSIID	DB	'0',13,10,0
TOWNSCD_MSG_PAUSED_REDIR			DB	"Paused SCSI CD Redirection.",13,10,0
TOWNSCD_MSG_RESUMEED_REDIR			DB	"Resumeed SCSI CD Redirection.",13,10,0
TOWNSCD_MSG_ERROR					DB	"Failed to enable SCSI CD Redirection.",13,10,0

TOWNSCD_WRITE:
						; Source https://www.drdobbs.com/writing-ms-dos-device-drivers/184402277?pgno=2
						; Source https://sites.google.com/site/pcdosretro/dostables
						MOV		CX,ES:[BX+REQ_READ_WRITE_LENGTH]	; Transfer Size
						LDS		SI,ES:[BX+REQ_READ_WRITE_BUFFER_PTR]	; Data Pointer

TOWNSCD_CMD_WRITE_LOOP:
						PUSH	CX
						LODSB

						; Case sensitive
						; ECHO E > TOWNS_CD   SCSI Enable Redirection (Use last SCSI ID)
						; ECHO e > TOWNS_CD   SCSI Enable Redirection (Use first SCSI ID)
						; ECHO 0 to 6 > TOWNS_CD  SCSI Enable Redirction with SCSI ID
						; ECHO D > TOWNS_CD   SCSI Pause Redirection
						; ECHO P > TOWNS_CD   SCSI Pause Redirection
						; ECHO R > TOWNS_CD   SCSI Resume Redirection

						CMP		AL,'E'
						JE		TOWNSCD_CMD_WRITE_CAPITAL_E
						CMP		AL,'e'
						JE		TOWNSCD_CMD_WRITE_SMALL_E
						CMP		AL,'D'
						JE		TOWNSCD_CMD_WRITE_PAUSE
						CMP		AL,'P'
						JE		TOWNSCD_CMD_WRITE_PAUSE
						CMP		AL,'R'
						JE		TOWNSCD_CMD_WRITE_RESUME
						MOV		AH,AL
						SUB		AH,'0'
						CMP		AH,6
						JBE		TOWNSCD_CMD_WRITE_SCSI_ID
						JMP		TOWNSCD_CMD_WRITE_LOOP_NEXT

TOWNSCD_CMD_WRITE_CAPITAL_E:
						CALL	TOWNSCD_ENABLE_REDIR_LAST_SCSIID
						JMP		TOWNSCD_CMD_WRITE_LOOP_NEXT

TOWNSCD_CMD_WRITE_SMALL_E:
						CALL	TOWNSCD_ENABLE_REDIR_FIRST_SCSIID
						JMP		TOWNSCD_CMD_WRITE_LOOP_NEXT

TOWNSCD_CMD_WRITE_SCSI_ID:
						SUB		AL,'0'
						CALL	TOWNSCD_ENABLE_REDIR_GIVEN_SCSIID
						JMP		TOWNSCD_CMD_WRITE_LOOP_NEXT

TOWNSCD_CMD_WRITE_PAUSE:
						CALL	TOWNSCD_PAUSE_REDIR
						JMP		TOWNSCD_CMD_WRITE_LOOP_NEXT

TOWNSCD_CMD_WRITE_RESUME:
						CALL	TOWNSCD_RESUME_REDIR
						JMP		TOWNSCD_CMD_WRITE_LOOP_NEXT

TOWNSCD_CMD_WRITE_LOOP_NEXT:
						POP		CX
						LES		BX,CS:[TOWNSCD_REQHDR_PTR]
						LOOP	TOWNSCD_CMD_WRITE_LOOP

						JMP		TOWNSCD_INTERRUPT_EXIT


TOWNSCD_ENABLE_REDIR_LAST_SCSIID:
						MOV		DL,6
TOWNSCD_ENABLE_REDIR_LAST_SCSIID_LOOP:
						MOV		AH,DISKBIOS_CMD_EXT_ENABLE_SCSI_REDIR
						MOV		AL,0C0H
						INT		93H
						JNC		TOWNSCD_REDIR_ENABLED
						DEC		DL
						JGE		TOWNSCD_ENABLE_REDIR_LAST_SCSIID_LOOP
						MOV		DX,TOWNSCD_MSG_ERROR
						JMP		TOWNSCD_MSG_AND_RET

TOWNSCD_ENABLE_REDIR_FIRST_SCSIID:
						XOR		DL,DL
TOWNSCD_ENABLE_REDIR_FIRST_SCSIID_LOOP:
						MOV		AH,DISKBIOS_CMD_EXT_ENABLE_SCSI_REDIR
						MOV		AL,0C0H
						INT		93H
						JNC		TOWNSCD_REDIR_ENABLED
						INC		DL
						CMP		DL,6
						JBE		TOWNSCD_ENABLE_REDIR_FIRST_SCSIID_LOOP
						MOV		DX,TOWNSCD_MSG_ERROR
						JMP		TOWNSCD_MSG_AND_RET

TOWNSCD_ENABLE_REDIR_GIVEN_SCSIID:
						MOV		DL,AL
						MOV		AH,DISKBIOS_CMD_EXT_ENABLE_SCSI_REDIR
						MOV		AL,0C0H
						INT		93H
						JNC		TOWNSCD_REDIR_ENABLED
						RET

TOWNSCD_PAUSE_REDIR:
						MOV		AH,DISKBIOS_CMD_EXT_DISABLE_SCSI_REDIR
						MOV		AL,0C0H
						INT		93H
						MOV		DX,TOWNSCD_MSG_PAUSED_REDIR
						JMP		TOWNSCD_MSG_AND_RET

TOWNSCD_RESUME_REDIR:
						MOV		AH,DISKBIOS_CMD_EXT_ENABLE_SCSI_REDIR
						MOV		AL,0C0H
						MOV		DL,0FFh
						INT		93H
						JNC		TOWNSCD_REDIR_ENABLED
						MOV		DX,TOWNSCD_MSG_ERROR
						JMP		TOWNSCD_MSG_AND_RET

TOWNSCD_REDIR_ENABLED:
						ADD		DL,'0'
						MOV		CS:[TOWNSCD_MSG_ENABLED_REDIR_SCSIID],DL
						MOV		DX,TOWNSCD_MSG_ENABLED_REDIR

TOWNSCD_MSG_AND_RET:
						PUSH	CS
						POP		DS

						MOV		BX,DX
TOWNSCD_MSG_AND_RET_COUT_LOOP:
						INC		BX
						CMP		BYTE [BX],0
						JNE		TOWNSCD_MSG_AND_RET_COUT_LOOP

						SUB		BX,DX
						MOV		CX,BX

						MOV		BX,2	; STDERR
						MOV		AH,40h
						INT		21H
						RET

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

TOWNSCD_IOCTL_READ:
						; SHUCDX expects this command to return the devhdr address.

						LDS		DI,ES:[BX+REQ_READ_WRITE_BUFFER_PTR]
						MOV		AL,[DI]
						CMP		AL,0
						JE		TOWNSCD_IOCTL_READ_DRVHDR
						CMP		AL,6
						JE		TOWNSCD_IOCTL_READ_DEVICE_STATUS
						CMP		AL,9
						JE		TOWNSCD_IOCTL_READ_MEDIA_CHANGED

						MOV		WORD ES:[BX+REQ_STATUS],MSCDEX_ERROR_UNKNOWN_CMD
						JMP		TOWNSCD_INTERRUPT_EXIT


TOWNSCD_IOCTL_READ_DRVHDR:
						MOV		WORD [DI+1],TOWNSCD_HEADER_NEXT
						MOV		WORD [DI+3],CS
						JMP		TOWNSCD_INTERRUPT_EXIT

TOWNSCD_IOCTL_READ_DEVICE_STATUS:
						; B0: Door Open
						; B1: Door Unlocked
						; B2: Support RAW Read
						; B3: Writable
						; B4: Can Play Audio
						; B5: Support Interleaving
						; B6: 0 unused
						; B7: Support Prefetching
						; B8: Support Audio Manipulation
						; B9: Support HSG addressing
						; B10-31: 0 unused
						; Towns OS V2.1 L20 TOWNS_CD was returning 00000612h Door Unlockd, Can Play Audio, Support Interleaving, + Unused??
						MOV		DWORD [DI+1],0612H
						JMP		TOWNSCD_INTERRUPT_EXIT

TOWNSCD_IOCTL_READ_MEDIA_CHANGED:
						; TOWNS_CD is using an unpublished function INT 93H AX=0FC0H to check media change.

						MOV		AX,0FC0H	; Unpublished function check media change.
						INT		93H

						MOV		AX,MSCDEX_MEDIA_CHANGED

						JB		TOWNSCD_IOCTL_READ_MEDIA_DID_CHANGE	; If error, just pretend media changed.

						AND		DL,DL
						JNE		TOWNSCD_IOCTL_READ_MEDIA_DID_CHANGE

						MOV		AX,MSCDEX_MEDIA_DID_NOT_CHANGE

TOWNSCD_IOCTL_READ_MEDIA_DID_CHANGE:

						LDS		DI,ES:[BX+REQ_READ_WRITE_BUFFER_PTR]
						MOV		WORD [DI+1],AX
						JMP		TOWNSCD_INTERRUPT_EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; SHSUCDX calls this function in the following way.
;27DE:00000CF6 89DF                      MOV     DI,BX
;27DE:00000CF8 BB6E03                    MOV     BX,036EH
;27DE:00000CFB 897F0E                    MOV     [BX+0EH],DI
;27DE:00000CFE 8C4710                    MOV     [BX+10H],ES
;27DE:00000D01 66894714                  MOV     [BX+14H],EAX
;27DE:00000D05 894F12                    MOV     [BX+12H],CX
;27DE:00000D08 1E                        PUSH    DS
;27DE:00000D09 07                        POP     ES
;27DE:00000D0A E8A7FF                    CALL    00000CB4
;27DE:00000D0D 817F030001                CMP     WORD PTR [BX+03H],0100H
;
;; In the LST.
;  2548                                  ; Parameters:
;  2549                                  ;	ES:BX -> buffer
;  2550                                  ;	  EAX := starting sector number
;  2551                                  ;	   CX := number of sectors
;
;  2562                                  CdReadLong
;  2563 00000BF3 065357                  	uses	es,bx,di
;  2564 00000BF6 89DF                    	mov	di, bx
;  2565 00000BF8 BB[6E02]                	mov	bx, rh_rl
;  2566 00000BFB 897F0E8C4710            	ses	di, bx+rhReadLong.Bufp
;  2567 00000C01 66894714                	mmovd	bx+rhReadLong.StartBlk
;  2568 00000C05 894F12                  	mov	[bx+rhReadLong.Count], cx
;  2569 00000C08 1E07                    	ld	es, ds
;  2570 00000C0A E8A7FF                  	call	DDCall
;  2571 00000C0D 817F030001              	cmpw	[bx+rh.Status], 100h
;  2572 00000C12 5F5B07C3                	return
;
;  ES:[BX+12H] must be sector count.
;  ES:[BX+0EH] must be buffer far pointer.
;  ES:[BX+14H] must be the starting sector.
TOWNSCD_READ_SECTORS:
						MOV		AX,05C0H
						MOV		ECX,ES:[BX+14H]
						MOV		DX,CX
						SHR		ECX,16
						XOR		CH,CH
						LDS		DI,ES:[BX+0EH]
						MOV		BX,ES:[BX+12H]	; Do it last.  ES:BX is destroyed.

						INT		93H
						JNC		TOWNSCD_INTERRUPT_EXIT

						CALL	TOWNSCD_BIOS_ERROR_TO_MSCDEX_ERROR

						LES		BX,CS:[TOWNSCD_REQHDR_PTR]
						MOV		ES:[BX+REQ_STATUS],AX

						JMP		TOWNSCD_INTERRUPT_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

TOWNSCD_NAZO_FUNCTION_82H:
						; TOWNS OS V2.1 L20's TOWNS_CD just returns status code 8103H.
						MOV		WORD ES:[BX+REQ_STATUS],8103H
						JMP		TOWNSCD_INTERRUPT_EXIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

TOWNSCD_BIOS_ERROR_TO_MSCDEX_ERROR:
						CMP		AH,080H
						JNE		TOWNSCD_BIOS_ERROR_UNKNOWN	; Unknown error

						CMP		CX,DISKBIOS_HARDERROR_DRIVE_NOT_READY
						JE		TOWNSCD_BIOS_ERROR_DRIVE_NOT_READY

						MOV		AX,800BH	; Read fault
						RET

TOWNSCD_BIOS_ERROR_UNKNOWN:
						MOV		AX,800BH	; Read fault or 800CH General Fault?
						RET

TOWNSCD_BIOS_ERROR_DRIVE_NOT_READY:
						MOV		AX,8002H	; Drive Not Ready
						RET

