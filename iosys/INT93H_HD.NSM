INT93H_HD:
						MOV		BYTE CS:[ERROR_CODE],80H
						MOV		WORD CS:[ERROR_DETAIL],0FFFFh

						MOV		AH,[BP+SAVED_EAX+1]
						CMP		AH,DISKBIOS_CMD_EXT_IDENTIFY_SCSI_DEV
						JE		INT93H_HD_IDENTIFY_DEVICE

						CMP		AH,3
						JE		INT93H_HD_03H_RESTORE
						CMP		AH,5
						JE		INT93H_HD_05H_READ
						CMP		AH,6
						JE		INT93H_HD_06H_WRITE

						JMP		INT93H_EXIT


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


INT93H_HD_03H_RESTORE:
						JMP		INT93H_EXIT_NO_ERROR


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


INT93H_HD_05H_READ:
						; Input
						;   CL    SCSI ID
						;   EDX   Starting Sector
						;   BX    Number of Sectors
						;   EDI   Data Buffer Physical Address
						;   DS=CS
						; Output
						;   CF    Set if error
						MOV		CL,[BP+SAVED_EAX]
						AND		CL,7

						MOV		DX,[BP+SAVED_ECX]
						SHL		EDX,16
						MOV		DX,[BP+SAVED_EDX]

						MOV		BX,[BP+SAVED_EBX]

						MOVZX	EAX,WORD [BP+SAVED_DS]
						SHL		EAX,4
						MOVZX	EDI,WORD [BP+SAVED_EDI]
						ADD		EDI,EAX

						PUSH	CS
						POP		DS

						CALL	SCSI_READ_SECTOR
						JC		INT93H_EXIT

						MOV		BYTE CS:[ERROR_CODE],0
						JMP		INT93H_EXIT


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


INT93H_HD_06H_WRITE:
						; Input
						;   CL    SCSI ID
						;   EDX   Starting Sector
						;   BX    Number of Sectors
						;   EDI   Data Buffer Physical Address
						;   DS=CS
						; Output
						;   CF    Set if error
						MOV		CL,[BP+SAVED_EAX]
						AND		CL,7

						MOV		DX,[BP+SAVED_ECX]
						SHL		EDX,16
						MOV		DX,[BP+SAVED_EDX]

						MOV		BX,[BP+SAVED_EBX]

						MOVZX	EAX,WORD [BP+SAVED_DS]
						SHL		EAX,4
						MOVZX	EDI,WORD [BP+SAVED_EDI]
						ADD		EDI,EAX

						PUSH	CS
						POP		DS

						CALL	SCSI_WRITE_SECTOR
						JC		INT93H_EXIT

						MOV		BYTE CS:[ERROR_CODE],0
						JMP		INT93H_EXIT


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


INT93H_HD_IDENTIFY_DEVICE:
						MOV		CL,[BP+SAVED_EAX]
						AND		CL,7
						; Input
						;   CL   SCSI ID
						; Output
						;   CL   05 or 04 means CD
						;        00 means HD
						;   Carry  Set if error
						CALL	IDENTIFY_SCSI_DEVICE
						JC		INT93H_EXIT

						MOV		BYTE [BP+SAVED_EAX],CL
						JMP		INT93H_EXIT_NO_ERROR
